
#new:NpcGroup $NpcGroup_GourmetGuy {
	00000000 $NpcSettings_GourmetGuy ~Vec3f:GourmetGuy
	00400D0D $Init_GourmetGuy 00000000 00000000 0000005A
	~NoDrops
	~Movement:GourmetGuy
	~AnimationTable:GourmetGuy
	00000001 00000000 00000000 00000000
}

#new:NpcSettings $NpcSettings_GourmetGuy {
	00000000 00200016 00000000 00000000
    00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}
#new:NpcGroup $NpcGroup_WeirdToad {
	00000001 $NpcSettings_WeirdToad ~Vec3f:WeirdToad
	00400D0D $Init_WeirdToad 00000000 00000000 0000005A
	~NoDrops
	~Movement:WeirdToad
	~AnimationTable:WeirdToad
	00000001 00000000 00000000 00000000
}

#new:Script $Init_GourmetGuy {
    Call BindNpcIdle ( .Npc:Self $Script_DisguiseAsGourmet )
	Return
	End
}

#new:Script $Init_WeirdToad {
    Call BindNpcIdle ( .Npc:Self $Script_ToadCutscene )
    Call BindNpcInteract ( .Npc:Self $Script_WeirdToadInteract )
    Return
    End
}

#new:Script $Script_WeirdToadInteract
{
    If *CorrectAnswer == 1
        If *GotMeat == 0
            Call SpeakToPlayer ( .Npc:Self 00830404 00830401 00000000 002F0014 )
            Call ShowGotItem ( .Item:KootRedJar .True 0 )
            Call AddKeyItem ( .Item:KootRedJar )
            Set *GotMeat 1
        Else
            Call SpeakToPlayer ( .Npc:Self 00830404 00830401 00000000 002F0015 )
        EndIf
    Else
        Call SpeakToPlayer ( .Npc:Self 00830404 00830401 00000000 002F0010 )
    EndIf
    Return
    End
}

#new:NpcSettings $NpcSettings_WeirdToad {
	00000000 001E0018 00000000 00000000
    00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:NpcGroupList $NpcGroupList_GourmetGuy {
    00000001 $NpcGroup_GourmetGuy 00000000
    00000001 $NpcGroup_WeirdToad 00000000
    00000000 00000000 00000000
}

#new:Script_Main $Script_Main
{
	Set   *GB_WorldLocation  .Location:ShootingStarSummit
    Set  *GF_Map_ShootingStarSummit .True
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True )
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Call  MakeNpcs ( .False $NpcGroupList_GourmetGuy )
	Exec  $Script_SetupMusic
    Call GetEntryID ( *Var[0] )
    If *Var[0] == 2
        Exec $Script_EntryFromPrison
    EndIf
	Exec  $Script_EnterMap
    Exec  $Script_MakeItemEntities
    Exec  $Script_MakeBinds
    If *StartedGame == 0
        Set *GB_StoryProgress 91
        Set *StartedGame 1
        Exec $Script_OpeningCutscene
    EndIf
    If *NoticedShyGuy == 1
        If *NoticedReturn == 0
            Exec $Script_ReturnToGourmet
        EndIf
    EndIf
	Return
	End
}

#new:Script $Script_ReturnToGourmet
{
    Wait 45`
    Call DisablePlayerInput ( .True )
    Call ShowMessageAtScreenPos ( 002F0017 160` 40` )
    Set *NoticedReturn 1
    Call DisablePlayerInput ( .False )
    Return
    End
}

#new:Script $Script_MakeBinds {
    Bind $Script_PickedShroomCake .Trigger:WallPressA ~Collider:ItemOneWall 1 0
    Bind $Script_PickedPotatoSalad .Trigger:WallPressA ~Collider:ItemTwoWall 1 0
    Return
    End
}

#new:Script $Script_PickedShroomCake {
    Set *Var[1] 0026003C
    Exec $Script_FoodChoice
    Return
    End
}

#new:Script $Script_PickedPotatoSalad {
    Set *Var[1] 00260054
    Exec $Script_FoodChoice
    Return
    End
}

#new:Script $Script_FoodChoice {
    Call DisablePlayerInput ( .True )
    Call SetMessageString ( *Var[1] 0 )
    Call SpeakToPlayer ( 1 00830404 00830401 00000000 002F0007 )
    Call ShowChoice ( 002F0008 )
    Call EndSpeech ( 1 00830404 00830401 00000000 )
    If *Var[0] == 00000000
        If *Var[1] == 00260054
            Call SpeakToPlayer ( 1 00830404 00830401 00000000 002F0009 )
            Exec $Script_CorrectAnswer
        Else
            Call SpeakToPlayer ( 1 00830404 00830401 00000000 002F000A )
            Exec $Script_IncorrectAnswer
        EndIf
    EndIf 
    Call DisablePlayerInput ( .False )
    Return
    End
}

#new:Script $Script_CorrectAnswer
{
    Call DisablePlayerInput ( .True )
    Set *Var[0] 0
    Call TranslateModel ( ~Model:ItemTwoFloorModel 0 -3` 0 )
    Call ParentColliderToModel ( ~Collider:ItemTwoFloor ~Model:ItemTwoFloorModel )
    Loop 25`
        SubF *Var[0] 3`
        Call TranslateModel ( ~Model:ItemTwoFloorModel 0 0 *Var[0] )
        Call ParentColliderToModel ( ~Collider:ItemTwoFloor ~Model:ItemTwoFloorModel )
        Wait 1`
    EndLoop
    Set *CorrectAnswer 1
    Call GotoMap ( "pri_00" 0 )
    Return
    End
}

#new:Script $Script_IncorrectAnswer
{
    Call DisablePlayerInput ( .True )
    Set *Var[0] 0
    Loop 25`
        Add *Var[0] 8`
        Call TranslateModel ( ~Model:ItemOneFloorModel 0 *Var[0] 0 )
        Call ParentColliderToModel ( ~Collider:ItemOneFloor ~Model:ItemOneFloorModel )
        Call DisablePlayerPhysics ( .True )
        Call GetPlayerPos ( *Var[1] *Var[2] *Var[3] )
        Add *Var[2] 8`
        Call SetPlayerPos ( *Var[1] *Var[2] *Var[3] )
        Wait 1`
    EndLoop
    Call GotoMap ( "hvn_00" 0 )
    Return
    End
}

#new:Script $Script_OpeningCutscene {
    Call DisablePlayerInput ( .True )
    Call ShowMessageAtScreenPos ( 002F0001 160` 40` )
    Call DisablePlayerInput ( .False )
    Return
    End
}

#new:Script $Script_ToadCutscene {
    If *TalkedToToad != 1
        Call AwaitPlayerApproach ( -671` -440` 300` )
        Call DisablePlayerInput ( .True )
        Call FadeOutMusic ( 0 500` )
        Call ShowMessageAtScreenPos ( 002F0004 160` 40` )
        Call SpeakToPlayer ( .Npc:Self 00830404 00830401 00000000 002F0005 )
        Call SetMusicTrack ( 0 .Song:KammyKoopaTheme 0 8 )
        Call PlayerMoveTo ( -566` -440` 90` )
        Call SpeakToPlayer ( .Npc:Self 00830404 00830401 00000000 002F0006 )
        Set *TalkedToToad 1
        Call DisablePlayerInput ( .False )
    Else
        If *CorrectAnswer == 1
            Call SetNpcAnimation ( .Npc:Self 00830405 )
        EndIf
    EndIf
    Return
    End
}

#new:Script $Script_DisguiseAsGourmet {
	Call $DisguiseAs ( .Npc:Self )
	Return
	End
}

#new:Script $Script_MakeItemEntities {
    Call MakeItemEntity ( .Item:ShroomCake ~Vec3d:ItemOne .ItemSpawnMode:Fixed_NeverVanish *ItemOnePickedUp )
    Call MakeItemEntity ( .Item:PotatoSalad ~Vec3d:ItemTwo .ItemSpawnMode:Fixed_NeverVanish *ItemTwoPickedUp )
    Return
    End
}

#new:Script $Script_CreateExitTriggers
{
	Return
	End
}

#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 2
}

#new:EntryList $EntryList
{
	~Vec4f:Entry0
	~Vec4f:Entry1
}

#new:Function_Init $Function_Init
{
	PUSH    RA
	LIA     A0, 800B0CF0
	LIA     A1, "kmr_tex"
	JAL     800654F0 % sprintf
	RESERVED
	CLEAR  V0
	JPOP   RA
}

#new:Script $Script_SetupMusic
{
    Call SetMusicTrack ( 0 .Song:SunTowerSunny 0 F )
	Return
	End
}

#new:Script $Script_EntryFromPrison
{
    Call DisablePlayerInput ( .True )
    Call DisablePlayerPhysics ( .True )
    Call SetPlayerPos ( ~Vec3d:Entry2 )
    Call SetPlayerJumpscale ( *Fixed[2.0] )
    Call PlayerJump ( 208` 0` 60` 30` )
    Call DisablePlayerPhysics ( .False )
    Call DisablePlayerInput ( .False )
    Return
    End
}

#new:Script $Script_EnterMap
{
	Return
	End
}
